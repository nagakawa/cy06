#TouhouDanmakufu[Player]
#ScriptVersion[3]
#ID["Player3"]
#Title["Esel Rygen B"]
#include "./player_magic_circle.dnh"
#include "./player_header.dnh"

let unfocusedSpeed = 4;
let focusedSpeed = 2;
let startingRadius = 8;
let hbSize = 2;
let playerID = 3;

task TImage {
	let path = GetCurrentScriptDirectory ~ "../resource/playershots.png";
	ObjPrim_SetTexture(objPlayer, path);
	ObjSprite2D_SetSourceRect(objPlayer, 0, 464, 21, 499);
	loop {
		ObjSprite2D_SetDestCenter(objPlayer);
		yield;
	}
}

let off = [
	ICE_RED, ICE_ORANGE, ICE_LIME, ICE_GREEN,
	ICE_AQUA, ICE_MINT, ICE_BLUE, ICE_PURPLE
];

task TShot {
	let counter = 0;
	let tick = 0;
	loop {
		let x = GetPlayerX;
		let y = GetPlayerY - 8;
		let shot = GetVirtualKeyState(VK_SHOT);
		if (shot == KEY_PUSH || shot == KEY_HOLD && tick == 0 &&
			GetPlayerState == STATE_NORMAL && IsPermitPlayerShot) {
			if (isFocused) {
				ObjRender_SetAlpha(createPlayerShotA1(x + 12 * sin(7 * counter), y, 7, 270, 8, 1, off[randIntH(0, 8)]), 80);
				ObjRender_SetAlpha(createPlayerShotA1(x - 12 * sin(7 * counter), y, 7, 270, 8, 1, off[randIntH(0, 8)]), 80);
			}
			else {
				ascent (i in 0 .. 7) {
					let a = counter + 360 * i / 7;
					ObjRender_SetAlpha(createPlayerShotA1(x, y, 10, 270 + 30 * sin(a), 15, 1, off[i]), 100 + 40 * cos(a));
				}
			}
		}
		counter += 1;
		if (counter >= 360) {counter -= 360;}
		tick++;
		if (tick == 6) {tick = 0;}
		yield;
	}
}

task starTask(shot) {
	let t = 0;
	let dm = rand(0.25, 0.5);
	while (!Obj_IsDeleted(shot) && t < 40) {
		ObjShot_SetDamage(shot, t * dm);
		t++;
		yield;
	}
}

task TSpell {
	let objManage = GetSpellManageObject;
	SetPlayerInvincibilityFrame(60);
	let width = GetStgFrameWidth;
	let height = GetStgFrameHeight;
	let x = GetPlayerX;
	let y = GetPlayerY;
	loop (100) {
		let star = CreatePlayerShotA1(x, y, rand(3.5, 4.5), rand(0, 360), rand(10, 20), 9999, STAR);
		ObjRender_SetColorHSV(star, rand(0, 360), 125, 255);
		ObjShot_SetEraseShot(star, true);
		starTask(star);
	}
	wait(90);
	Obj_Delete(objManage);
}
