#include "./shader/utils.dnh"

task initColorfulShader {
	let s = initShader("colorful", "TecColorful");
	cycle(s);
}

task cycle(s) {
	let f = rand(0, 360);
	while (!Obj_IsDeleted(s)) {
		ObjShader_SetFloat(s, "frame", f);
		f++;
		yield;
	}
}

function createRect(left, right, top, bottom) {
	return createRectR(left, right, top, bottom, 0.89);
}

function createRectR(left, right, top, bottom, rp) {
	let obj = ObjPrim_Create(OBJ_PRIMITIVE_2D);
	ObjPrim_SetPrimitiveType(obj, PRIMITIVE_TRIANGLESTRIP);
	ObjPrim_SetVertexCount(obj, 4);
	ObjPrim_SetVertexPosition(obj, 0, left, top, 0);
	ObjPrim_SetVertexPosition(obj, 1, right, top, 0);
	ObjPrim_SetVertexPosition(obj, 2, left, bottom, 0);
	ObjPrim_SetVertexPosition(obj, 3, right, bottom, 0);
	ascent (i in 0 .. 4) {
		ObjPrim_SetVertexUV(obj, i, i % 2, truncate(i / 2));
		ObjPrim_SetVertexColor(obj, i, 0, 0, 0);
		ObjPrim_SetVertexAlpha(obj, i, 190 + 20 * (i > 0) + 20 * (i == 3));
	}
	Obj_SetRenderPriority(obj, rp);
	ObjShader_SetShaderO(obj,
		GetAreaCommonData("Shader", "Colorful", "Call initColorfulShader!"));
	return obj;
}