#include "./spell_header.dnh"
#include "./sp_ceri.dnh"

let level = 1;
let bossn = 0;
let id = 0;

@Event {
	alternative (GetEventType)
	case (EV_REQUEST_LIFE) {
		SetScriptResult(4000);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(32);
	}
}

task shootFrom(x, y, type) {
	let a = rand(0, 360);
	let t = 0;
	shotSound;
	while (t < 60 && !isDead) {
		let r = getRank;
		let dt = max(3, truncate(9 - 1.2 * r));
		ascent (i in 0 .. 3 + r) {
			CreateShotA1(x, y, 2.4 + 0.2 * i, a, type, 5);
			CreateShotA1(x, y, 2.4 + 0.2 * i, a + 180, type, 5);
		}
		a += 6 * dt;
		t += dt;
		wait(dt);
	}
}

task move {
	let a = 270;
	let bx = ObjMove_GetX(objBoss);
	let by = ObjMove_GetY(objBoss) + 60;
	while (!isDead) {
		ObjMove_SetPosition(objBoss, bx + 120 * cos(a), by + 60 * sin(a));
		a += 2;
		yield;
	}
}

task TWork {
	move;
	while (!isDead) {
		shootFrom(
			ObjMove_GetX(objBoss) + rand(-120, 120),
			ObjMove_GetY(objBoss) + rand(-30, 90),
			[SCALE_RED, SCALE_ORANGE, SCALE_YELLOW][randIntH(0, 3)]
		);
		wait(max(30, 40 - 2 * getRank));
	}
}