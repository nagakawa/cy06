
function loadingImage(name) {
	let img = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(img, GetCurrentScriptDirectory ~ "../resource/" ~ name ~ ".png");
	ObjSprite2D_SetSourceRect(img, 1, 0, 128, 63);
	ObjSprite2D_SetDestRect(img, 601, 520, 768, 583);
	Obj_SetRenderPriority(img, 0.95);
	return img;
}

function loadingText {
	let t = cta(S("loading"));
	txp(601, 530);
	txct(255, 255, 255);
	txcb(224, 231, 255);
	txh(18);
	txt("menu");
	txb(0, 0, 0);
	txr(96);
	return t;
}

function fileText {
	let t = cta(S("loading"));
	txp(641, 550);
	txct(255, 255, 255);
	txcb(224, 231, 255);
	txh(10);
	txt("dialogue");
	txb(0, 0, 0);
	txr(96);
	return t;
}

function load(img, sound, script) {
	let done = false;
	let fnameText = fileText;
	task loadingMessage {
		let triangle = loadingImage("loading");
		let aura = loadingImage("aura");
		ObjRender_SetBlendType(aura, BLEND_ADD_ARGB);
		let t = loadingText;
		let ctr = 0;
		while (!done) {
			ctr++;
			ObjRender_SetAlpha(aura, 128 + 127 * sin(6 * ctr));
			yield;
		}
		Obj_Delete(triangle);
		Obj_Delete(aura);
		Obj_Delete(t);
	}
	loadingMessage;
	let l = length(img);
	ascent (i in 0 .. l) {
		let name = img[i];
		LoadTextureInLoadThread(GetCurrentScriptDirectory ~ "../resource/" ~ name);
		ObjText_SetText(fnameText, name);
		yield;
	}
	l = length(sound);
	ascent (i in 0 .. l) {
		let name = sound[i];
		LoadSound(GetCurrentScriptDirectory ~ "../se/" ~ name);
		ObjText_SetText(fnameText, name);
		yield;
	}
	l = length(script);
	ascent (i in 0 .. l) {
		let name = script[i];
		LoadScriptInThread(GetCurrentScriptDirectory ~ "../" ~ name);
		ObjText_SetText(fnameText, name);
		yield;
	}
	done = true;
	Obj_Delete(fnameText);
}

function loadWithDefaults {
	load(
		[
			"cenvos.png",
			"enemy.png",
			"hurtbox.png",
			"hurtbox_center.png",
			"items.png",
			"laser.png",
			"playershots.png",
			"nesmeja.png",
			"laser.png",
			"../hele/bullets.png",
			"../ambient/shotsheet.png"
		],
		[
			"beep11.wav",
			"cut00.wav",
			"eco01.wav",
			"freeze07.wav",
			"loop001.wav",
			"on01.wav",
			"paper00.wav",
			"power14.wav",
			"sha00.wav"
		],
		[
			
		]
	);
}