#include "../propkg.dnh"
#include "./ingame_scenes.dnh"
#include "./loading.dnh"

let rpi = -1;
let difficulty;
let character;
let stage = 0;
let start;
let end;
let i;

@Initialize {
	rpi = GetScriptArgument(0);
	difficulty = GetScriptArgument(1);
	character = GetScriptArgument(2);
	if (GetScriptArgumentCount <= 3) {
		stage = GetScriptArgument(3);
	}
	startMe(rpi, difficulty, character, stage);
	main;
}

let textures = [
	[
		"mb3d/Rock_04_UV_H_CM_1.jpg",
		"rentex/09_DIFFUSE.jpg"
	]
];

function startStage(stage) {
	SetStageIndex(i);
	load(
		textures[stage - 1],
		[],
		[]
	);
}

function startMe(rpi_, difficulty_, character_, stage_) {
	rpi = rpi_;
	difficulty = difficulty_;
	character = character_;
	stage = stage_;
	let current = GetCurrentScriptDirectory;
	if (stage == 0) {
		start = 1;
		end = 6;
	} else {
		start = stage;
		end = stage;
	}
	InitializeStageScene;
	i = start;
	let mainPath = current ~ "../stage/" ~ vtos("02d", i) ~ ".dnh";
	let playerPath = current ~ "../player/player" ~ IntToString(character_) ~ ".dnh";
	WriteLog(playerPath);
	SetStagePlayerScript(playerPath);
	startStage(i);
	SetStageMainScript(mainPath);
	setDifficulty(difficulty);
	setCharacter(character);
	setMode(stage);
	SetCommonData("Start", true);
	if (rpi != -1) {
		let path = GetReplayInfo(rpi, REPLAY_FILE_PATH);
		SetStageReplayFile(path);
	}
	StartStageScene;
}

sub endScene {
	if (rpi != -1 || !TEndScene(stage, rpi, difficulty, character)) {
		playBGM(0);
		TerminateStageScene;
		CloseScript(GetOwnScriptID);
	}
}

task main {
	while (!IsCloseScript(GetOwnScriptID)) {
		if (GetVirtualKeyState(VK_PAUSE) == KEY_PUSH && !GetCommonData("pauseblocked", false)) {
			let res = RunPauseScene;
			alternative (res)
			case (RESULT_RETRY) {
				if (!IsReplay) {
					TerminateStageScene;
					startMe(-1, difficulty, character, stage);
					main;
					return;
				}
			}
			case (RESULT_END) {
				TerminateStageScene;
				playBGM(0);
				CloseScript(GetOwnScriptID);
				return;
			}
		}
		let state = GetStageSceneState;
		if (state == STAGE_STATE_FINISHED) {
			let res = GetStageSceneResult;
			alternative (res)
			case (STAGE_RESULT_CLEARED) {
				if (i == end) {
					endScene;
					return;
				} else {
					i++;
					startStage(i);
					SetCommonData("Start", false);
					let mainPath = GetCurrentScriptDirectory ~ "../stage/" ~ vtos("02d", i) ~ ".dnh";
					SetStageMainScript(mainPath);
					StartStageScene;
				}
			}
			case (STAGE_RESULT_PLAYER_DOWN) {
				endScene;
				return;
			}
			case (STAGE_RESULT_BREAK_OFF) {
				playBGM(0);
				TerminateStageScene;
				CloseScript(GetOwnScriptID);
			}
		}
		yield;
	}
}

@MainLoop {
	yield;
}

@Finalize {
	playBGM(0);
}