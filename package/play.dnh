#include "../propkg.dnh"
#include "./ingame_scenes.dnh"
#include "./loading.dnh"
#include "../system/transparent_background.dnh"

let rpi = -1;
let difficulty;
let character;
let start = 1;
let end = 6;
let mode;
let i;
let alreadyLoaded = [false, false, false, false, false, false, false, false];

@Initialize {
	rpi = GetScriptArgument(0);
	difficulty = GetScriptArgument(1);
	character = GetScriptArgument(2);
	mode = GetScriptArgument(3);
	if (GetScriptArgumentCount >= 6) {
		start = GetScriptArgument(4);
		end = GetScriptArgument(5);
	}
	startMe(rpi, difficulty, character, mode, start, end);
	main;
}

let textures = [
	[
		"mb3d/Rock_04_UV_H_CM_1.jpg",
		"rentex/09_DIFFUSE.jpg",
		"../ambient/spritesheet/b0.png",
		"../ambient/portrait/b0_0.png"
	]
];

function startStage(stage) {
	SetStageIndex(i);
	let res = alreadyLoaded[stage - 1];
	if (!res) {
		load(
			textures[stage - 1],
			[],
			[]
		);
		alreadyLoaded[stage - 1] = true;
	}
	return res;
}

function startMe(rpi_, difficulty_, character_, mode_, start_, end_) {
	rpi = rpi_;
	difficulty = difficulty_;
	character = character_;
	mode = mode_;
	let current = GetCurrentScriptDirectory;
	InitializeStageScene;
	WriteLog("initialized stage scene");
	i = start;
	let mainPath = current ~ "../stage/" ~ vtos("02d", i) ~ ".dnh";
	let playerPath = current ~ "../player/player" ~ IntToString(character_) ~ ".dnh";
	SetStagePlayerScript(playerPath);
	WriteLog("player set");
	startStage(i);
	SetStageMainScript(mainPath);
	WriteLog("main set");
	setDifficulty(difficulty);
	setCharacter(character);
	setMode(mode);
	WriteLog("DCM set");
	SetCommonData("Start", true);
	if (rpi != -1) {
		let path = GetReplayInfo(rpi, REPLAY_FILE_PATH);
		SetStageReplayFile(path);
		loadStageStats(start, difficulty);
	}
	WriteLog("replay set");
	StartStageScene;
	WriteLog("blastoff!");
}

sub endScene {
	if (rpi != -1 || !TEndScene(mode, rpi, difficulty, character)) {
		playBGM(0);
		TerminateStageScene;
		CloseScript(GetOwnScriptID);
	}
}

task main {
	while (!IsCloseScript(GetOwnScriptID)) {
		if (GetVirtualKeyState(VK_PAUSE) == KEY_PUSH && !GetCommonData("pauseblocked", false)) {
			let res = RunPauseScene;
			alternative (res)
			case (RESULT_RETRY) {
				if (!IsReplay) {
					TerminateStageScene;
					startMe(-1, difficulty, character, mode, start, end);
					main;
					return;
				}
			}
			case (RESULT_END) {
				TerminateStageScene;
				playBGM(0);
				CloseScript(GetOwnScriptID);
				return;
			}
		}
		if (isDedz) {
			dedz(false);
			endScene;
			return;
		}
		let state = GetStageSceneState;
		if (state == STAGE_STATE_FINISHED) {
			let res = GetStageSceneResult;
			alternative (res)
			case (STAGE_RESULT_CLEARED) {
				if (i == end) {
					endScene;
					return;
				} else {
					i++;
					startStage(i);
					SetCommonData("Start", false);
					let mainPath = GetCurrentScriptDirectory ~ "../stage/" ~ vtos("02d", i) ~ ".dnh";
					SetStageMainScript(mainPath);
					StartStageScene;
				}
			}
			case (STAGE_RESULT_PLAYER_DOWN) {
				endScene;
				return;
			}
			case (STAGE_RESULT_BREAK_OFF) {
				playBGM(0);
				TerminateStageScene;
				CloseScript(GetOwnScriptID);
			}
		}
		yield;
	}
}

@MainLoop {
	yield;
}

@Finalize {
	playBGM(0);
}

task loadStageStats(stage, difficulty) {
	let s = IntToString(stage);
	SetAreaCommonData(
		"Replay", "Power",
		GetAreaCommonData("Replay", "Power" ~ s, 0)
	);
	SetAreaCommonData(
		"Replay", "Radius",
		GetAreaCommonData("Replay", "Radius" ~ s, 5)
	);
	SetAreaCommonData(
		"Replay", "Value",
		GetAreaCommonData("Replay", "Value" ~ s, 0.01)
	);
	SetAreaCommonData(
		"Replay", "Trance",
		GetAreaCommonData("Replay", "Trance" ~ s, 0)
	);
	SetAreaCommonData(
		"Replay", "Rank",
		GetAreaCommonData("Replay", "Rank" ~ s, difficulty)
	);
}

function isDedz {
	return GetCommonData("Dedz", false);
}

task dedz(d) {
	SetCommonData("Dedz", d);
}