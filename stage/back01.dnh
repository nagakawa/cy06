#include "./back_header.dnh"

let randomValues = [0.3];
let seeds = [];
let sky = GetCurrentScriptDirectory ~ "../resource/luv.png";
let spr;

task initBG {
	ascent (i in 0 .. 32) {
		randomValues = randomValues ~
			[
				rand(-1, 1),
				rand(-1, 1),
				rand(-1, 1),
				rand(-1, 1),
				rand(-1, 1),
				rand(-1, 1),
				rand(-1, 1),
				rand(-1, 1)
			];
	}
	ascent (i in 0 .. 8) {
		seeds = seeds ~ [truncate(rand(16384, 32768))];
	}
	if (getEffectCut == 4) {
		CloseScript(GetOwnScriptID);
		return;
	}
	let start = GetPackageTime;
	let waterPath = GetCurrentScriptDirectory ~ "../resource/rentex/09_DIFFUSE.jpg";
	spr = ObjPrim_Create(OBJ_PRIMITIVE_3D);
	ObjPrim_SetTexture(spr, waterPath);
	ObjPrim_SetPrimitiveType(spr, PRIMITIVE_TRIANGLESTRIP);
	ObjPrim_SetVertexCount(spr, 16);
	rectangularW(spr, 560, -50, 20, 560, 100, 20, 560, -50, 10, 560, 100, 10, 2, 1, 0);
	rectangularW(spr, 570, -50, 20, 570, 100, 20, 570, -50, 10, 570, 100, 10, 2, 1, 4);
	rectangularW(spr, 570, -50, 10, 570, 100, 10, 560, -50, 10, 560, 100, 10, 2, 1, 8);
	rectangularW(spr, 570, -50, 20, 570, 100, 20, 560, -50, 20, 560, 100, 20, 2, 1, 12);
	ObjRender_SetColor(spr, 64, 128, 255);
	ascent (i in 0 .. 4) { flow(spr, 4 * i); }
	Obj_SetRenderPriority(spr, 0.21);
	invisibleOnSpell(spr);
	createCaveMesh(20, 20, seeds[0], seeds[1], seeds[2]);
	createCaveMesh(-20, 20, seeds[3], seeds[4], seeds[5]);
	setFog;
	WriteLog("It took " ~ IntToString(GetPackageTime - start) ~ " ms to initialize the background.");
	//debug;
}

task setFog {
	SetFogEnable(true);
	ascent (i in 0 .. 120) {
		SetFogParam(2.75 * i, 3 * i, 0, 0, 0);
		yield;
	}
}

task customBehavior {
	SetCameraRadius(1);
	SetCameraAzimuthAngle(1);
	SetCameraElevationAngle(0);
	SetCameraYaw(180);
	SetCameraFocusXYZ(-250, 5, 0);
	let i = -1600;
	while (!IsCloseScript(GetOwnScriptID)) {
		if (i <= 1600) {SetCameraFocusX(i / 6.4);}
		SetCameraRoll(45 * sin(i + 1600));
		i++;
		yield;
	}
}

let MESH_SIZE = 256;

function createCaveMesh(y, res, seed1, seed2, seed3) {
	let px = MESH_SIZE / res;
	let verticesPerRow = 1 + 4 * res;
	let vertexTotal = 2 * res * (verticesPerRow + 1) + 1;
	let seed4 = seeds[5];
	let seed5 = seeds[6];
	let seed6 = seeds[7];
	let obj = ObjPrim_Create(OBJ_PRIMITIVE_3D);
	ObjPrim_SetPrimitiveType(obj, PRIMITIVE_TRIANGLESTRIP);
	ObjPrim_SetTexture(obj, GetCurrentScriptDirectory ~ 
		"../resource/mb3d/Rock_04_UV_H_CM_1.jpg");
	ObjPrim_SetVertexCount(obj, vertexTotal);
	function setVertex(vidx, i, j) {
		let offset = (seed1 * i * i + seed2 * j * j + seed3 * i * j) % 256;
		let x = i * px + px * (randomValues[(offset + 7) % 256]) ^ 2;
		let z = j * px + px * (randomValues[(offset + 14) % 256]) ^ 2;
		let dy = 100 * (randomValues[offset]) ^ 4;
		if (absolute(j) <= 5) {
			dy *= 0.2 + 0.032 * j ^ 2;
		}
		else if ((seed4 * i * i + seed5 * j * j + seed6 * i * j) % 256 < 3) {
			dy *= 2 + 0.8 * randomValues[(offset + 98) % 256];
		}
		let Y = y + dy;
		let lr = 1 + randomValues[(offset + 209) % 256] * 0.1;
		let lg = 1 + randomValues[(offset + 210) % 256] * 0.1;
		let lb = 1 + randomValues[(offset + 21) % 256] * 0.1;
		ObjPrim_SetVertexPosition(obj, vidx, x, Y, z);
		ObjPrim_SetVertexUV(obj, vidx,
			0.01 * x + 0.006 * randomValues[(offset + 74) % 256],
			0.01 * z + 0.006 * randomValues[(offset + 74) % 256]
		);
		ObjPrim_SetVertexColor(obj, vidx, 105 * lr, 94 * lg, 78 * lb);
	}
	let vidx = 0;
	ascent (i in -res / 2 .. res / 2) {
		ascent (j in -res .. res + 1) {
			setVertex(vidx, 2 * i, j);
			setVertex(vidx + 1, 2 * i + 1, j);
			vidx += 2;
		}
		descent (j in -res .. res + 1) {
			setVertex(vidx, 2 * i + 1, j);
			setVertex(vidx + 1, 2 * i + 2, j);
			vidx += 2;
		}
	}
	setVertex(vidx, res, res);
	WriteLog("Debug: " ~ IntToString(vertexTotal) ~ " vs " ~ IntToString(vidx + 1));
	Obj_SetRenderPriority(obj, 0.21);
	invisibleOnSpell(obj);
	ObjRender_SetZWrite(obj, true);
	ObjRender_SetZTest(obj, true);
	return obj;
}

task flow(water, i) {
	while (GetCameraFocusX < 250) {yield;}
	let u = 0;
	while (exists(water)) {
		ObjPrim_SetVertexUV(water, i, u, 0);
		ObjPrim_SetVertexUV(water, i + 1, u + 2, 0);
		ObjPrim_SetVertexUV(water, i + 2, u + 2, 1);
		ObjPrim_SetVertexUV(water, i + 3, u, 1);
		u += 1 / 60;
		yield;
	}
}