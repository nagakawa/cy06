#include "../prostg.dnh"
#include "../itsbloc.dnh"

let cwd = GetCurrentScriptDirectory;

@Initialize {
	if (getDifficulty == -2) {
		LoadCommonDataAreaFromReplayFile("Replay");
		let difficulty = GetAreaCommonData("Replay", "Difficulty", 1);
		setDifficulty(difficulty);
	}
	else {
		SetAreaCommonData("Replay", "Difficulty", getDifficulty);
	}
	if (GetCommonData("Start", false)) {reset;}
	SetCommonData("Dialogue", false);
	StartItemScript(cwd ~ "item/system_item.dnh");
	let path = cwd ~ "../player/playersd.dnh";
	LoadEnemyShotData(path);
	initFrame;
	score(false);
	score(true);
	tPower;
	radius;
	value;
	trance;
	mech;
}

@MainLoop {yield;}

function initFrame {
	let path = cwd ~ "../resource/stgframe.png";
	let obj = ObjPrim_Create(OBJ_SPRITE_2D);
	ObjPrim_SetTexture(obj, path);
	Obj_SetRenderPriority(obj, 0.05);
	ObjSprite2D_SetSourceRect(obj, 0, 0, 800, 600);
	ObjSprite2D_SetDestRect(obj, 0, 0, 800, 600);
	SetStgFrame(16, 16, 484, 584, 20, 80);
}

task reset {
	yield;
	setPower(0);
	setRadius(5);
	setValue(0.01);
	AddGraze(-GetGraze);
	SetCommonData("pauseblocked", false);
}

function createNumberObject(x, y, tr, tg, tb, mw) {
	let obj = ObjText_Create;
	ObjRender_SetBlendType(obj, BLEND_ALPHA);
	Obj_SetRenderPriority(obj, 0.1);
	ObjRender_SetY(obj, y);
	ObjRender_SetX(obj, x);
	ObjText_SetMaxWidth(obj, mw);
	ObjText_SetHorizontalAlignment(obj, ALIGNMENT_RIGHT);
	ObjText_SetFontType(obj, S("font.score"));
	ObjText_SetFontSize(obj, 24);
	ObjText_SetFontColorTop(obj, tr, tg, tb);
	ObjText_SetFontColorBottom(obj, 255, 255, 255);
	ObjText_SetFontBorderType(obj, BORDER_FULL);
	ObjText_SetFontBorderWidth(obj, 2);
	ObjText_SetFontBorderColor(obj, 0, 0, 0);
	Obj_SetRenderPriority(obj, 0.19);
	return obj;
}

function updateInt(obj, n) {
	let text = IntToStringIL(n);
	ObjText_SetText(obj, text);
}

function updateReal(obj, n, prec) {
	let text = RealToStringIL(n, prec);
	ObjText_SetText(obj, text);
}

function createText(x, y, tr, tg, tb, key) {
	let obj = cta(S(key));
	txp(x, y);
	txct(tr, tg, tb);
	txcb(255, 255, 255);
	txb(0, 0, 0);
	txh(24);
	txt("charIntro");
	txr(19);
	return obj;
}

function getHighScore {
	let hs = GetAreaCommonData("Cy06",
		"HS" ~ IntToString(getCharacter) ~ IntToString(getDifficulty), [[1024]])[0][0];
	return max(GetScore, hs);
}

task score(isHigh) {
	let text = createText(525, 90 - 50 * isHigh, 128, 255, 128, ["stgf.score", "stgf.hiscore"][isHigh]);
	let num = createNumberObject(525, 120 - 50 * isHigh, 128, 255, 128, 250);
	while (!IsCloseScript(GetOwnScriptID)) {
		updateInt(num, [GetScore, getHighScore][isHigh]);
		yield;
	}
}

task tPower {
	let text = createText(525, 140, 255, 128, 128, "stgf.power");
	let num = createNumberObject(525, 170, 255, 128, 128, 250);
	while (!IsCloseScript(GetOwnScriptID)) {
		updateReal(num, getPower, 2);
		yield;
	}
}

task radius {
	let text = createText(525, 190, 128, 128, 255, "stgf.radius");
	let num = createNumberObject(525, 220, 128, 128, 255, 250);
	while (!IsCloseScript(GetOwnScriptID)) {
		updateReal(num, getRadius, 2);
		yield;
	}
}

task value {
	let text = createText(525, 240, 128, 255, 255, "stgf.value");
	let num = createNumberObject(525, 270, 128, 255, 255, 250);
	while (!IsCloseScript(GetOwnScriptID)) {
		updateReal(num, getValue, sinterval);
		yield;
	}
}

task trance {
	let text = createText(525, 290, 192, 128, 192, "stgf.trance");
	let num = createNumberObject(525, 320, 192, 128, 192, 250);
	while (!IsCloseScript(GetOwnScriptID)) {
		let normalized = getTrancePercentage * base ^ 2 / 100;
		ObjText_SetText(num, IntToStringIL(normalized) ~ S("stgf.trance.suffix"));
		yield;
	}
}

task mech {
	let player = GetPlayerObjectID;
	while (!IsCloseScript(GetOwnScriptID)) {
		AddScore(-GetScore);
		AddScore(GetGraze);
		if (getTrancePercentage >= 100) {
			activateTrance(true);
		} else if (getTrancePercentage <= 0) {
			setTrancePercentage(0);
			activateTrance(false);
		}
		if (isTranceActive) {
			addTrancePercentage(-1 / 6);
		}
		yield;
	}
}